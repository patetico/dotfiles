#!/bin/zsh

eval "$(sheldon source)"
eval "$(starship init zsh)"
eval "$(zoxide init zsh)"

setopt long_list_jobs       # show long list format job notifications
setopt interactivecomments  # recognize comments
setopt autocd               # cd if possible when command not found

## Load init files on zshrc.d
function {
  local init_file
  for init_file in ${ZDOTDIR}/zshrc.d/*.zsh; do
    source "$init_file"
  done
}

function zreload() {
  local zsh="${ZSH_ARGZERO#-}"
  # Check whether to run a login shell
  [[ -o login ]] && exec -l "$zsh" || exec "$zsh"
}

function {
  # remove duplicates in path just in case
  local -a tmp_path
  local p

  for p in $path; do
    (( ! $tmp_path[(Ie)$p] )) && tmp_path+=("$p");
  done

  path=($tmp_path)
}

function add_path() {
  (( ! $path[(Ie)$1] )) && path+=("$1");
}

function _try_source() {
  [[ -f "$1" ]] && source "$1"
}

function _try_lazy_source() {
  [[ -f "$1" ]] && zsh-defer source "$1"
}

# nvm
function {
  local src_fn=_try_lazy_source

	# the JetBrains terminal crashes if nvm initialization is defered
  [[ "$TERMINAL_EMULATOR" == 'JetBrains-JediTerm'  ]] && src_fn=_try_source
  
	$src_fn /usr/share/nvm/init-nvm.sh || {
    $src_fn "${NVM_DIR}/nvm.sh"
    $src_fn "${NVM_DIR}/bash_completion"
  }
}

[[ -n $PNPM_HOME ]] && add_path "$PNPM_HOME"            # pnpm
[[ -n $GOPATH ]] && add_path "${GOPATH}/bin"            # golang
add_path "${XDG_DATA_HOME}/JetBrains/Toolbox/scripts"   # JetBrains IDEs

hascmd bat && {
  export MANPAGER="sh -c 'col -bx | bat -l man -p'"
  export MANROFFOPT="-c"
}

_try_source "${DOTDIR:-$HOME}/.config/zsh/.zshrc.local"

unfunction _try_source _try_lazy_source
